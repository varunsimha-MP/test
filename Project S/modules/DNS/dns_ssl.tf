resource "aws_route53_zone" "domain" {
    name = var.domain_name
}

resource "aws_route53_record" "subdomain" {
    zone_id = aws_route53_zone.domain.zone_id
    name =  "${var.subdomain_name}.${var.domain_name}"
    type = "A"
    ttl = 300
    records = [var.public_ip]
}

resource "aws_acm_certificate" "ssl" {
    domain_name = var.domain_name
    validation_method = "DNS"
    lifecycle {
      create_before_destroy = true
    }
    tags = var.acm_name
}

resource "aws_route53_record" "cert_validation" {
    for_each = {
        for dvo in aws_acm_certificate.ssl.domain_validation_options : dvo.domain_name => {
            name = dvo.resource_record_name
            record = dvo.resource_record_value
            type = dvo.resource_record_type
        }
    }
    zone_id = aws_route53_zone.domain.zone_id
    name = each.value.name
    records = [each.value.record]
    ttl = 60
    type = each.value.type
    allow_overwrite = true
}

resource "aws_acm_certificate_validation" "validation" {
    certificate_arn = aws_acm_certificate.ssl.arn 
    validation_record_fqdns = [for record in aws_route53_record.cert_validation : record.fqdns]
}